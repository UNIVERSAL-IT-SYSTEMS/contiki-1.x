
   10 REM LOADER FOR CONTIKI ON THE APPLE ][ (OLIVER SCHMIDT)   
   20 HOME :VTAB 2:HTAB 12
   30 PRINT "WELCOME TO CONTIKI"
   40 VTAB 3:HTAB 12
   50 PRINT "=================="
   60 FOR I=0 TO 6
   70 C$=C$+CHR$(PEEK(640+I))
   80 NEXT I
   90 IF C$<>"CONTIKI" GOTO 150
  100 VTAB 19:HTAB 10
  110 PRINT "SAVING CONTIKI CONF..."
  120 PRINT CHR$(4)"BSAVE CONTIKI.CONF,A$280,L$80"
  130 VTAB 19:HTAB 10
  140 PRINT "                      "
  150 IF PEEK(116)>=184 GOTO 190
  160 HOME :PRINT 
  170 PRINT "ERROR: CONTIKI REQUIRES DOS TO BE IN LC"
  180 END 
  190 VTAB 8:HTAB 5
  200 PRINT "1: 40 COL. (64K APPLE ][ OR ][+)"
  210 VTAB 10:HTAB 5
  220 PRINT "2: 80 COL. (ENHANCED //E OR //C)"
  230 VTAB 13:HTAB 5
  240 PRINT "0: QUIT"
  250 VTAB 16:HTAB 5
  260 GET K$
  270 IF K$<"0" OR K$>"2" GOTO 260
  280 IF K$="0" GOTO 410
  290 IF K$="1" GOTO 310
  300 E$=".ENH"
  310 IF C$="CONTIKI" GOTO 350
  320 VTAB 19:HTAB 10
  330 PRINT "LOADING CONTIKI CONF..."
  340 PRINT CHR$(4)"BLOAD CONTIKI.CONF"
  350 VTAB 19:HTAB 10
  360 PRINT "LOADING CONTIKI DATA..."
  370 PRINT CHR$(4)"BRUN CONTIKI.DATA"E$
  380 VTAB 19:HTAB 10
  390 PRINT "LOADING CONTIKI CODE..."
  400 PRINT CHR$(4)"BRUN CONTIKI.CODE"E$
  410 HOME 

