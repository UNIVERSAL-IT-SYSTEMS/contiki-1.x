
   10 REM LOADER FOR CONTIKI ON THE APPLE ][ (OLIVER SCHMIDT)   
   20 HOME :VTAB 2:HTAB 12
   30 PRINT "Welcome to Contiki"
   40 VTAB 3:HTAB 12
   50 PRINT "=================="
   60 FOR I=0 TO 6
   70 C$=C$+CHR$(PEEK(640+I))
   80 NEXT I
   90 IF C$<>"CONTIKI" GOTO 150
  100 VTAB 19:HTAB 10
  110 PRINT "Saving Contiki Conf..."
  120 PRINT CHR$(4)"BSAVE CONTIKI.CONF,A$280,L$80"
  130 VTAB 19:HTAB 10
  140 PRINT "                      "
  150 IF PEEK(116)>=184 GOTO 190
  160 HOME :PRINT 
  170 PRINT "Error: Contiki requires DOS to be in LC"
  180 END 
  190 VTAB 8:HTAB 11
  200 PRINT "1: 40 Column Display"
  210 VTAB 10:HTAB 11
  220 PRINT "2: 80 Column Display"
  230 VTAB 13:HTAB 11
  240 PRINT "0: Quit"
  250 VTAB 16:HTAB 11
  260 GET K$
  270 IF K$<"0" OR K$>"2" GOTO 260
  280 IF K$="0" GOTO 440
  290 IF K$="2" GOTO 320
  300 H=10
  310 GOTO 340
  320 PRINT :PRINT CHR$(4)"PR#3":PRINT 
  330 H=28
  340 IF C$="CONTIKI" GOTO 380
  350 VTAB 19:HTAB H
  360 PRINT "Loading Contiki Conf..."
  370 PRINT CHR$(4)"BLOAD CONTIKI.CONF"
  380 VTAB 19:HTAB H
  390 PRINT "Loading Contiki Data..."
  400 PRINT CHR$(4)"BRUN CONTIKI.DATA"
  410 VTAB 19:HTAB H
  420 PRINT "Loading Contiki Code..."
  430 PRINT CHR$(4)"BRUN CONTIKI.CODE"
  440 HOME 

